group 'ar.edu.unrc'
version '1.0.0'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}

//https://docs.gradle.org/current/userguide/standard_plugins.html
apply plugin: 'idea'
apply plugin: 'java'
//apply plugin: 'findbugs'
apply plugin: 'application' //https://docs.gradle.org/current/userguide/application_plugin.html
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'distribution'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

javadoc {
    options.encoding = 'UTF-8'
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile group: 'ar.edu.unrc', name: 'coeus', version: '1.0.0'
    compile group: 'org.encog', name: 'encog-core', version: '3.3.0'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://clojars.org/repo/" }
    }
}

mainClassName = "ar.edu.unrc.coeus.pig.Game"

//create a single Jar with all dependencies
task finalFatJar(type: Jar) {
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Application-Name': 'Easy Pig',
                'Implementation-Version': version,
                'Built-By': 'Lucia Bressan, Renzo Bianchini, Franco Gast√≥n Pellegrini',
                'Built-Date': new Date(),
                'Built-Java': System.getProperty('java.version'),
                'Main-Class': mainClassName,
                'Sealed': 'true')
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

configure(finalFatJar) {
    group = 'distribution'
    description = 'Create a big Jar with all dependency'
}

